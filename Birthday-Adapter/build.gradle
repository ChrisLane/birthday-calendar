apply plugin: 'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:24.0.0' // 25.0.0 does not support minSdkVersion 7!
    compile 'com.android.support:preference-v7:24.0.0'
    compile 'org.sufficientlysecure:html-textview:2.0'
    compile 'com.pavelsikun:vintage-chroma:1.5'
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 24
        versionCode 18
        versionName "1.17"
        applicationId "org.birthdayadapter"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    productFlavors {
        full {
            applicationId = "org.birthdayadapter"
            buildConfigField "boolean", "FULL_VERSION", "true"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "false"
        }
        full_google {
            applicationId = "org.birthdayadapter"
            buildConfigField "boolean", "FULL_VERSION", "true"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "true"
        }
        free {
            applicationId = "org.birthdayadapter.free"
            buildConfigField "boolean", "FULL_VERSION", "false"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "true"
        }
    }
}